#!/bin/awk -f
# grurls - substitute and group urls

BEGIN {
	i = 0
}
END {
	for (l = 0; l < i; l++) {
		printf "[%d] %s\n", l, links[l]
	}
}
{
	# [title](url)
	while (match($0, /\[[^]]*\]\([^)]*\)/)) {
		lnk = substr($0, RSTART, RLENGTH)
		match(lnk, /\[[^]]*\]/)
		t = substr(lnk, RSTART+1, RLENGTH-2)
		match(lnk,/\([^)]*\)/)
		links[i] = substr(lnk, RSTART+1, RLENGTH-2)
		sub(/\[[^]]*\]\([^)]*\)/, t " [" i++ "]")
	}
	# http[s]://foo/bar
	while (match($0, /http[s]?:\/\/[^ ]*/)) {
		links[i] = substr($0, RSTART, RLENGTH)
		sub(/http[s]?:\/\/[^ ]*/, "[" i++ "]")
	}
	print
}
